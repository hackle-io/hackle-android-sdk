plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

allprojects {
    group = 'io.hackle'
    version = '1.0.0'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName version

        buildConfigField "String", "VERSION_NAME", "\"$version\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    api 'io.hackle:hackle-sdk-common:1.0.0'
    implementation 'io.hackle:hackle-sdk-core:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact sourceJar

                pom {
                    name = project.name
                    description = 'Hackle Android Sdk'
                    url = 'http://www.hackle.io'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Hackle'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/hackle-io/hackle-android-sdk.git'
                        developerConnection = 'scm:git:https://github.com/hackle-io/hackle-android-sdk.git'
                        url = 'https://github.com/hackle-io/hackle-android-sdk'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "MavenCentral"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv("SONATYPE_USERNAME")
                    password = System.getenv("SONATYPE_PASSWORD")
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}